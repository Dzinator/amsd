namespace amsd.model;

class AppointmentManagementSystem

{
     singleton;
     1 -> 0..* Person;
     1 -> 0..* Appointment;
     1 -> 0..* PatientProfile;
     1 -> 0..* DentistProfile;
     1 -> 0..* HygienistProfile;
}

 

 

class Person {
     name;
     
     before setName {
     if (aName == null) {
			return false; }
     }
     
     
  	 phoneNumber;
     1 -- 0..1 PatientProfile;
     1 -- 0..1 EmployeeProfile;
}

 

class PatientProfile

{
	depend amsd.controller.*;
     Integer missedAppointments;
     before setMissedAppointments {
     	if( aMissedAppointments < 0){
     		return false;
     	}
     }
     1 -- * Appointment;

	public Appointment getAppointment(Date date, int time){
	    for(Appointment app : getAppointments()){
		  if(Controller.datesEqual(app.getDate(),date) && app.getTime() == time){
			  return app;
		  }
		}
	  return null;
  	}
  	
  	sm{
  		Allowed{
  			miss (Appointment app) [missedAppointments >= 2] ->  NotAllowed;
  		}
  		NotAllowed{
  			payFee (Appointment app) [missedAppointments < 2] -> Allowed;
  		}
  	}
  	
  		before miss {
  			missedAppointments++; 
  			app.miss();
  		};
  		before payFee {
  			missedAppointments--;
  			app.payfee();
  		};

}

 

class EmployeeProfile

{
	depend amsd.controller.*;
     1 -- * Appointment;
     1 -- * Availability;
     
       public Availability getAvailability(Date date, int time){
    for(Availability avail : getAvailabilities()){
		  if(Controller.datesEqual(date,avail.getDate()) && avail.getTime() == time){
			  return avail;
		  }
		}
	  return null;
  }
  	
  	public Appointment getAppointment(Date date, int time){
	    for(Appointment app : getAppointments()){
		  if(Controller.datesEqual(app.getDate(),date) && app.getTime() == time){
			  return app;
		  }
		}
	  return null;
  	}

}

 

class DentistProfile

{

     isA EmployeeProfile;

}

 

class HygienistProfile

{

     isA EmployeeProfile;

}

 

class Appointment

{
	 final long twoDaysInMillis = 1000*60*60*24*2;
     Date date;
     Integer time;

  

    sm{

      Booked {
          cancel(long today) [date - today >= twoDaysInMillis] -> Canceled;
          miss -> Missed;
          attend -> Attended;
      }

      Canceled {
      	entry / {
	  		this.getAvailability().cancel();
      		//this.delete();
      	}
      }

      Attended {
      }

      Missed {
        payfee -> FeePaid;

      }

      FeePaid {
      }

    }
   /* 
     @Override
  public boolean equals(Object o){
	  if(!(o instanceof Appointment)){
		  return false;
	  }
	  
	  Appointment a = (Appointment) o;
	  return a.getDate().equals(this.getDate()) && a.getTime() == this.getTime() &&
			  a.getEmployeeProfile().equals(this.getEmployeeProfile()) &&
			  a.getPatientProfile().equals(this.getPatientProfile());
  }
  */

}

 

 

class Availability

{

 sm{
      Available{
        book(Appointment a) /{setAppointment(a);}-> Booked ;
      }

      Booked {
      	cancel -> Available;
      }
      
   }
     1 -- 0..1 Appointment;
     Date date;
     Integer time;
}